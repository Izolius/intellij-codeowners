// https://docs.github.com/en/github/creating-cloning-and-archiving-repositories/about-code-owners#codeowners-syntax
{
  parserClass               = "com.github.fantom.codeowners.lang.kind.github.parser.CodeownersParser"
  extends                   = "com.github.fantom.codeowners.lang.CodeownersElementImpl"

  psiClassPrefix            = "Codeowners"
  psiImplClassSuffix        = "Impl"
  psiPackage                = "com.github.fantom.codeowners.lang.kind.github.psi"
  psiImplPackage            = "com.github.fantom.codeowners.lang.kind.github.psi.impl"

  elementTypeHolderClass    = "com.github.fantom.codeowners.lang.kind.github.psi.CodeownersTypes"
  elementTypeClass          = "com.github.fantom.codeowners.lang.kind.github.psi.CodeownersElementType"
//  elementTypeClass          = "com.github.fantom.codeowners.lang.CodeownersElementType"
  tokenTypeClass            = "com.github.fantom.codeowners.lang.kind.github.psi.CodeownersTokenType"
//  tokenTypeClass            = "com.github.fantom.codeowners.lang.CodeownersTokenType"

  tokens = [
    CRLF                    = "regexp:[\s\r\n]+"
    HEADER                  = "regexp:###.*"
    SECTION                 = "regexp:##.*"
    COMMENT                 = "regexp:#.*"
    SLASH                   = "/"
    AT                      = "@"
//    TEAMNAME                = "regexp:[\w-]+"
//    USERNAME                = "regexp:[\w\d-]+"
//    DOMAIN                  = "regexp:\w+(\.\w+)+"
    VALUE                   = "regexp:[^@\s/]+"
    PATHNAME                = "regexp:([^\s/]|\\\s)+"
    SPACES                  = 'regexp:[\s\t\f]+'
  ]

  name("Pattern.*")         = "pattern"
  mixin("Pattern")          = "com.github.fantom.codeowners.lang.kind.github.psi.impl.CodeownersPatternExtImpl"
  mixin("NamedOwner")       = "com.github.fantom.codeowners.lang.kind.github.psi.impl.CodeownersNamedOwnerExtImpl"
  implements("Pattern")     = "com.github.fantom.codeowners.lang.CodeownersPatternBase"
  implements("Rule")        = "com.github.fantom.codeowners.lang.kind.github.psi.CodeownersRuleBase"
  mixin("Rule")             = "com.github.fantom.codeowners.lang.kind.github.psi.impl.CodeownersRuleExtImpl"
}

codeownersFile          ::= item_ *
private item_           ::= HEADER | SECTION | COMMENT | Rule | CRLF

Reset                   ::= (PatternDirectory | PatternFile)

Assign                  ::= (PatternDirectory | PatternFile) SPACES Owners

Rule                    ::= Assign | Reset

Pattern                 ::= '/' ? <<list_macro PATHNAME>>
PatternDirectory        ::= '/' ? <<list_macro PATHNAME>> '/' { extends = "PatternFile"}
PatternFile             ::= '/' ? <<list_macro PATHNAME>>     { extends = "Pattern"}

Owners                  ::= Owner (SPACES Owner)*
Owner                   ::= Email | NamedOwner
NamedOwner              ::= '@' OwnerName

OwnerName               ::= Team | UserName
Team                    ::= OrgName '/' TeamName

Email                   ::= UserName '@' Domain

UserName                ::= VALUE
OrgName                 ::= VALUE
TeamName                ::= VALUE
Domain                  ::= VALUE

private meta list_macro ::= <<p>> + ('/' <<p>> +) *
