{
  parserClass               = "com.github.fantom.codeowners.language.parser.CodeownersParser"
  extends                   = "com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix            = "Codeowners"
  psiImplClassSuffix        = "Impl"
  psiPackage                = "com.github.fantom.codeowners.language.psi"
  psiImplPackage            = "com.github.fantom.codeowners.language.psi.impl"

  elementTypeHolderClass    = "com.github.fantom.codeowners.language.psi.CodeownersTypes"
  elementTypeClass          = "com.github.fantom.codeowners.language.psi.CodeownersElementType"
  tokenTypeClass            = "com.github.fantom.codeowners.language.psi.CodeownersTokenType"

  tokens = [
    CRLF                    = "regexp:[\s\r\n]+"
    HEADER                  = "regexp:###.*"
    SECTION                 = "regexp:##.*"
    COMMENT                 = "regexp:#.*"
    SLASH                   = "/"
    AT                      = "@"
//    TEAMNAME                = "regexp:[\w-]+"
//    USERNAME                = "regexp:[\w\d-]+"
//    DOMAIN                  = "regexp:\w+(\.\w+)+"
    VALUE                   = "regexp:[^@/\s]+"
//    SPACES                  = 'regexp:\s+'
  ]

  name("Entry.*")           = "entry"
  mixin("Entry")            = "com.github.fantom.codeowners.language.psi.impl.CodeownersEntryExtImpl"
  mixin("NamedOwner")       = "com.github.fantom.codeowners.language.psi.impl.CodeownersNamedOwnerExtImpl"
  implements("Entry")       = "com.github.fantom.codeowners.language.psi.CodeownersEntryBase"
}

codeownersFile          ::= item_ *
private item_           ::= HEADER | SECTION | COMMENT | Pattern | CRLF

Pattern                 ::= (EntryDirectory | EntryFile) /*SPACES*/ CRLF Owners /*CRLF*/

Entry                   ::= '/' ? <<list_macro VALUE>>
EntryDirectory          ::= '/' ? <<list_macro VALUE>> '/' { extends = "EntryFile"}
EntryFile               ::= '/' ? <<list_macro VALUE>>     { extends = "Entry"}

Owners                  ::= Owner (CRLF Owner)*
Owner                   ::= Email | NamedOwner
NamedOwner              ::= '@' OwnerName

OwnerName               ::= Team | UserName
Team                    ::= OrgName '/' TeamName

Email                   ::= UserName '@' Domain

UserName                ::= VALUE
OrgName                 ::= VALUE
TeamName                ::= VALUE
Domain                  ::= VALUE

private meta list_macro ::= <<p>> + ('/' <<p>> +) *
